#!/opt/local/bin/perl
#
# Connect a normal Unix program to the slack chat system.
# SSL errors require Crypt::SSLeay, so we include it here.
#
use warnings;
use strict;
use FindBin '$Bin';
use IPC::Open2;
use Fcntl;
use Data::Dumper;
use LWP::UserAgent;
use Getopt::Long qw(:config no_ignore_case require_order);
use JSON;

use Net::SSL;
my $lwp = LWP::UserAgent->new(
	agent	=> "slack-wrap/0.0000",
	ssl_opts => { verify_hostname => 0 }, # shouldn't do this
);

my $json = JSON->new->utf8->pretty(1);

my $usage = <<"";
Usage: $0 [options] -- cmd args...
Options:
  -v | --verbose         Print debugging info on packets
  -t | --token token     Authorization token (default is to read from file)
  -c | --channel id      Channel ID (required)
  -l | --list-channels   List channels
  -u | --user name       User name to use for the bot
  -i | --interval secs   Time between polls (default 0.5 seconds)
  -I | --ignore RE       Regular expression to ignore on commands
  -F | --filter RE       Regular expression to apply to output from command
  -s | --stdin           Read/write from stdin/stdout instead of a command
  -p | --print-user      Print the user name of the poster

my $log = \*STDERR;
my $base_url = "https://slack.com/api";
my $token;
my $channel;
my $user;
my $filter;
my @ignore;
my $sleep_interval = 0.5;
my $use_stdin;
my $print_user;
my $list_channels;
my $verbose;

GetOptions(
	"t|token=s"	=> \$token,
	"c|channel=s"	=> \$channel,
	"l|list-channels+" => \$list_channels,
	"u|username=s"	=> \$user,
	"i|interval=f"	=> \$sleep_interval,
	"I|ignore=s"	=> \@ignore,
	"F|filter=s"	=> \$filter,
	"s|stdin+"	=> \$use_stdin,
	"v|verbose+"	=> \$verbose,
	"p|print-user+"	=> \$print_user,
	"h|?|help"	=> sub { print $usage; exit 0; },
) or die $usage;

# Read the token from a file, unless it was specified on the command line
$token ||= do "$Bin/slack.token";

if ($list_channels)
{
	my $rc = $lwp->post("$base_url/channels.list", {
		token => $token,
	} ) or die "unable to get channels $!\n";

	my $j = $json->decode($rc->decoded_content);
	print STDERR Dumper($j)
		if $verbose;

	my $channels = $j->{channels}
		or die "Unknown response?\n";

	for my $c (@$channels)
	{
		my $name = $c->{name};
		my $id = $c->{id};
		print "$name: $id\n";
	}
	
	exit 0;
}

die "Channel must be specified!\n"
	unless defined $channel;

my $cmd;
if ($use_stdin)
{
	die "Excess arguments?\n" if @ARGV;
} else {
	$cmd = shift
		or die "Command must be specified!\n";
}

# compile all of the ignore regular expressions
for (@ignore)
{
	$_ = qr/$_/i;
}

# compile the filtering regular expressions
$filter = eval "sub { local \$_ = shift; $filter; return \$_; }"
	if defined $filter;

$user ||= $cmd;

my $in = \*STDIN;
my $out = \*STDOUT;

unless($use_stdin)
{
	my $pid = open2($in, $out, $cmd, @ARGV)
			or die "exec '$cmd': failed: $!\n";
}

sub nonblock
{
	my $in = shift;
	my $old_flags = fcntl($in, F_GETFL, 0);
	fcntl($in, F_SETFL, $old_flags | O_NONBLOCK);
}

nonblock($in);
nonblock($out);

sub check_for_output
{
	my $in = shift;
	my $line;
	sysread $in, $line, 1024;

	return $line;
}

my $last_time = time();


sub ignore
{
	my $text = shift;

	for my $re (@ignore)
	{
		return 1 if $text =~ $re;
	}

	# none of the ignore regular expressions matched,
	# process this one
	return 0;
}


sub check_for_input
{
	my $rc = $lwp->post("$base_url/channels.history", {
		token	=> $token,
		channel	=> $channel,
		oldest => $last_time,
		count => 1000,
	}) or die "unable to get: $!\n";

	unless ($rc->is_success)
	{
		die "get failed\n";
	}

	my $j = $json->decode($rc->decoded_content);
	my @lines;

	#print Dumper($j->{ok});
	#print Dumper($j->{messages});
	#print Dumper($j->{has_more});
	my $lines = 0;
	my $total_lines = 0;

	# ignore empty messages
	return unless @{$j->{messages}};

	for my $msg (@{$j->{messages}})
	{
		print STDERR Dumper($msg), "\n"
			if $verbose;

		$total_lines++;
		if ($msg->{ts} > $last_time)
		{
			warn "ts: $msg->{ts} > $last_time\n";
			$last_time = $msg->{ts};
		}

		if ($msg->{subtype})
		{
			# ignore all subtyped messages
			next;
			next if $msg->{subtype} eq 'bot_message';
		}

		my $text = $msg->{text};

		next if ignore($text);

		$text = $msg->{user} . ": $text"
			if $print_user;

		push @lines, $text;
		$lines++;
	}

	warn "$lines/$total_lines\n";
	return @lines;
}


sub send_to_slack
{
	my $text = shift;

	# reformat the text to avoid reflows; broken
	#$text =~ s/(?<!\n)\n(?!\n)/ /msg;

	my $rc = $lwp->post("$base_url/chat.postMessage", {
		token => $token,
		channel => $channel,
		text => $text,
		username => $user,
		#as_user => "false",
	} ) or die "unable to post: $!\n";

	print "post:", Dumper($rc);
}


while(1)
{
	my $data = check_for_output($in);
	if ($data)
	{
		print "read:\n$data\n----\n" if $data;

		$data = $filter->($data) if defined $filter;

		send_to_slack($data);
	}

	my @lines = check_for_input(\*STDIN);
	print $out "$_\n" for @lines;

	sleep($sleep_interval);
}
